#include "sdl.ceu"

input void SDL_REDRAW;
input void SDL_QUIT;

native do
    SDL_Renderer* REN = NULL;
    int REN_W, REN_H;
end

var _SDL_Window& win;
    finalize
        win = _SDL_CreateWindow("SDL 5", _SDL_WINDOWPOS_CENTERED,
                                         _SDL_WINDOWPOS_CENTERED,
                                         800, 480,
                                         _SDL_WINDOW_SHOWN);
    with
        _SDL_DestroyWindow(&win);
    end

_SDL_GetWindowSize(&win, &_REN_W, &_REN_H);

finalize
    _REN = _SDL_CreateRenderer(&win, -1, 0);
with
    _SDL_DestroyRenderer(_REN);
end

var _SDL_Rect bg;
    bg.x = 0;
    bg.y = 0;
    bg.w = 640;
    bg.h = 480;

var _SDL_Texture& tex;
    finalize
        tex = _IMG_LoadTexture(_REN, "samples/img.png");
    with
        _SDL_DestroyTexture(&tex);
    end
var _SDL_Rect tex_r;
    tex_r.x = 100;
    tex_r.y = 100;
    tex_r.w = 20;
    tex_r.h = 20;

_TTF_Init();
    finalize with
        _TTF_Quit();
    end

var _TTF_Font& fnt;
    finalize
        fnt = _TTF_OpenFont("samples/Deutsch.ttf", 40);
    with
        _TTF_CloseFont(&fnt);
    end

var _SDL_Texture& str_tex;
var _SDL_Rect str_r;
    str_r.x = 10;
    str_r.y = 10;
    str_r.w = 200;
    str_r.h = 20;
do
    var _SDL_Color clr;
        clr.r = 255;
        clr.g = 255;
        clr.b = 255;
    var _SDL_Surface* sfc;
    finalize
        sfc = _TTF_RenderText_Blended(&fnt, "Hello world!", clr);
    with
        _SDL_FreeSurface(sfc);
    end
    finalize
        str_tex = _SDL_CreateTextureFromSurface(_REN, sfc);
    with
        _SDL_DestroyTexture(&str_tex);
    end
end

_Mix_OpenAudio(22050, _AUDIO_S16SYS, 2, 4096);
    finalize with
        _Mix_CloseAudio();
    end
var _Mix_Chunk& snd;
    finalize
        snd = _Mix_LoadWAV("samples/click1.wav");
    with
        _Mix_FreeChunk(&snd);
    end

par/or do
    await SDL_QUIT;
with
    every 5s do
        _Mix_PlayChannel(-1, &snd, 0);
    end
with
    every SDL_REDRAW do
        _SDL_SetRenderDrawColor(_REN, 0x00,0x00,0x00,0xFF);
        _SDL_RenderFillRect(_REN, &bg);
        _SDL_RenderCopy(_REN, &tex, null, &tex_r);
        _SDL_RenderCopy(_REN, &str_tex, null, &str_r);
        _SDL_RenderPresent(_REN);
    end
end

escape 0;

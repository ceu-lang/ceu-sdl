#ifndef ALL

#include "sdl.ceu";

input void SDL_QUIT;
input void SDL_REDRAW;

var _SDL_Window[] win;
finalize
    win = _SDL_CreateWindow("SDL 03", 200,200, 640,480, 0);
with
    _SDL_DestroyWindow(win);
end

var _SDL_Renderer[] ren;
finalize
    ren = _SDL_CreateRenderer(win, -1, 0);
with
    _SDL_DestroyRenderer(ren);
end

#else

_SDL_SetWindowTitle(win, "SDL 03");

#endif

class Rect with
    var _SDL_Renderer[] ren;
    var _SDL_Rect r;
    var int dt;
do
    this.r.x = 100;
    this.r.w =  20;
    this.r.h =  20;

    par do
        every (this.dt)ms do
            this.r.x = this.r.x + 1;
        end
    with
        every SDL_REDRAW do
            _SDL_SetRenderDrawColor(this.ren, 0xFF,0xFF,0xFF,0);
            _SDL_RenderFillRect(this.ren, &r);
        end
    end
end

par/or do
    every SDL_REDRAW do
        _SDL_SetRenderDrawColor(ren, 0x00,0x00,0x00, 0);
        _SDL_RenderFillRect(ren, null);
    end
with
    var Rect r1 with
        this.ren = ren;
        this.r.y = 100;
        this.dt  =  15;
    end;
    var Rect r2 with
        this.ren = ren;
        this.r.y = 300;
        this.dt  =  10;
    end;
    await FOREVER;
with
    every SDL_REDRAW do
        _SDL_RenderPresent(ren);
    end
with
    await SDL_QUIT;
end

escape 0;
